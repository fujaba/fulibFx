plugins {
    id "java"
    id 'maven-publish'
    id 'signing'
}

group = projectGroup
version = processorVersion
description = 'Annotation Processor for fulibFx. Checks for common errors and creates compilation errors and warnings.'

// ------------------- Dependencies -------------------

repositories {
    mavenCentral()
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

// Project dependencies
dependencies {
    implementation group: 'com.google.auto.service', name: 'auto-service-annotations', version: autoServiceVersion
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: autoServiceVersion

    implementation project(':framework')
}

// ------------------- Build -------------------

java {
    sourceCompatibility = getVersionForMajor(javaSourceVersion)
    targetCompatibility = getVersionForMajor(javaTargetVersion)

    withSourcesJar()
    withJavadocJar()
}

static JavaVersion getVersionForMajor(String version) {
    return JavaVersion.values().find { (it.majorVersion == version) }
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = "UTF-8"
    (options as StandardJavadocDocletOptions).addBooleanOption('Xdoclint:all,-missing', true)
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    manifest {
        attributes(
                'Manifest-Version': version,
                'Class-Path': '.',
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// ------------------- Publishing -------------------

publishing.publications.create('mavenJava', MavenPublication) {
    it.artifactId = 'fulibFx-processor'
    it.from(components.java)
    it.pom {
        name = 'fulibFx-processor'
        description = project.description
        url = 'https://github.com/fujaba/fulibFx'
        inceptionYear = '2024'

        scm {
            url = 'https://github.com/fujaba/fulibFx'
        }

        licenses {
            license {
                name = 'MIT License'
                url = 'https://www.opensource.org/licenses/mit-license.php'
            }
        }

        developers {
            developer {
                id = 'LeStegii'
                name = 'Paul Mertens'
            }
            developer {
                id = 'Clashsoft'
                name = 'Adrian Kunz'
            }
        }
    }
}

signing.sign publishing.publications.mavenJava
