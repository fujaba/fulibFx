plugins {
    id "java"
    id 'maven-publish'
    id 'signing'
}

group = projectGroup
version = project.findProperty('processorVersion') ?: getGitVersion()

String getGitVersion() {
    String output = 'git describe --tags --always'.execute([], rootDir).text.trim()
    if (output.startsWith('v')) {
        output = output.substring(1)
    }
    return output
}

// ------------------- Dependencies -------------------

repositories {
    mavenCentral()
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

// Project dependencies
dependencies {
    // https://mvnrepository.com/artifact/com.google.auto.service/auto-service-annotations
    implementation 'com.google.auto.service:auto-service-annotations:1.1.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'

    // Framework
    implementation project(":framework")
}

// ------------------- Build -------------------

java {
    sourceCompatibility = getVersionForMajor(javaSourceVersion)
    targetCompatibility = getVersionForMajor(javaTargetVersion)

    withSourcesJar()
    withJavadocJar()
}

static JavaVersion getVersionForMajor(String version) {
    return JavaVersion.values().find { (it.majorVersion == version) }
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = "UTF-8"
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    manifest {
        attributes(
                'Manifest-Version': version,
                'Class-Path': '.',
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// ------------------- Publishing -------------------

publishing.publications.create('mavenJava', MavenPublication) {
    it.artifactId = 'fulibFx-processor'
    it.from(components.java)
    it.pom {
        name = 'fulibFx-processor'
        description = project.description
        url = 'https://github.com/fujaba/fulibFx'
        inceptionYear = '2023'

        scm {
            url = 'https://github.com/fujaba/fulibFx'
        }

        licenses {
            license {
                name = 'MIT License'
                url = 'https://www.opensource.org/licenses/mit-license.php'
            }
        }

        developers {
            developer {
                id = 'LeStegii'
                name = 'Paul Mertens'
            }
            developer {
                id = 'Clashsoft'
                name = 'Adrian Kunz'
            }
        }
    }
}

signing.sign publishing.publications.mavenJava
