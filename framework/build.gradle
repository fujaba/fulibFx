plugins {
    id "java"
    id 'maven-publish'
    id 'signing'
    // https://plugins.gradle.org/plugin/org.openjfx.javafxplugin
    id "org.openjfx.javafxplugin" version "0.1.0"
}

group = projectGroup
version = frameworkVersion
description = 'A versatile Framework for JavaFX applications specifically designed for MVC pattern projects.'

// ------------------- Dependencies -------------------

repositories {
    mavenCentral()
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

// JavaFX dependencies
javafx {
    version = '21.0.2'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.media']
}

// Project dependencies
dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    // https://mvnrepository.com/artifact/com.google.dagger/dagger
    implementation group: 'com.google.dagger', name: 'dagger', version: '2.51'

    // https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler
    annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '2.51'

    // https://mvnrepository.com/artifact/io.reactivex.rxjava3/rxjava
    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.8'
}

// ------------------- Tests -------------------

// Test dependencies
dependencies {

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.1'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.1'

    // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.17'

    // https://mvnrepository.com/artifact/org.testfx/openjfx-monocle
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: '21.0.2'

    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.11.0'

    // https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler
    testAnnotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '2.51'

}

test {
    useJUnitPlatform()
}

// ------------------- Build -------------------

java {
    sourceCompatibility = getVersionForMajor(javaSourceVersion)
    targetCompatibility = getVersionForMajor(javaTargetVersion)

    withSourcesJar()
    withJavadocJar()
}

static JavaVersion getVersionForMajor(String version) {
    return JavaVersion.values().find { (it.majorVersion == version) }
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = "UTF-8"
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    manifest {
        attributes(
                'Manifest-Version': version,
                'Class-Path': '.',
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// ------------------- Publishing -------------------

publishing.publications.create('mavenJava', MavenPublication) {
    it.artifactId = 'fulibFx'
    it.from(components.java)
    it.pom {
        name = 'fulibFx'
        description = project.description
        url = 'https://github.com/fujaba/fulibFx'
        inceptionYear = '2023'

        scm {
            url = 'https://github.com/fujaba/fulibFx'
        }

        licenses {
            license {
                name = 'MIT License'
                url = 'https://www.opensource.org/licenses/mit-license.php'
            }
        }

        developers {
            developer {
                id = 'LeStegii'
                name = 'Paul Mertens'
            }
            developer {
                id = 'Clashsoft'
                name = 'Adrian Kunz'
            }
        }
    }
}

signing.sign publishing.publications.mavenJava
